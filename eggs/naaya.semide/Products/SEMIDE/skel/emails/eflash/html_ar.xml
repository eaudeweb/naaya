<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<!--Page translations-->
<!--section titles, these should be translated for each language-->
<!--These should have a case defined in the "headings" template-->
<xsl:variable name="headline">الافتتاحية</xsl:variable>
<xsl:variable name="inbrief">مختصرات</xsl:variable>
<xsl:variable name="nominations">تعيينات و وظائف شاغرة</xsl:variable>
<xsl:variable name="publications">إصدارات</xsl:variable>
<xsl:variable name="proposal">إعلانات</xsl:variable>
<xsl:variable name="papers">طلب مشاركات</xsl:variable>
<xsl:variable name="training">Training and courses</xsl:variable>
<xsl:variable name="events">أحداث</xsl:variable>
<xsl:variable name="sitemap">أحداث</xsl:variable>
<xsl:variable name="contact">اتصل بنا</xsl:variable>
<!--end section titles-->

<xsl:template name="trans_toc">Table of content</xsl:template>
<xsl:template name="trans_publisher">Publisher</xsl:template>
<xsl:template name="trans_link">Link</xsl:template>
<xsl:template name="trans_further_info">Further information on </xsl:template>
<xsl:template name="trans_further_site">EMWIS website</xsl:template>
<!--End of page translations-->

<xsl:template name="headings"><!--template used for section headings (in TOC and in the listing of documents)-->
	<xsl:choose><!--Set section heading depending on 'id' attribute-->
		<xsl:when test="@id='headline'"><xsl:value-of select="$headline" /></xsl:when>
		<xsl:when test="@id='inbrief'"><xsl:value-of select="$inbrief" /></xsl:when>
		<xsl:when test="@id='nominations'"><xsl:value-of select="$nominations" /></xsl:when>
		<xsl:when test="@id='publications'"><xsl:value-of select="$publications" /></xsl:when>
		<xsl:when test="@id='proposal'"><xsl:value-of select="$proposal" /></xsl:when>
		<xsl:when test="@id='papers'"><xsl:value-of select="$papers" /></xsl:when>
		<xsl:when test="@id='training'"><xsl:value-of select="$training" /></xsl:when>
		<xsl:when test="@id='events'"><xsl:value-of select="$events" /></xsl:when>
		<xsl:when test="@id='sitemap'"><xsl:value-of select="$sitemap" /></xsl:when>
		<xsl:when test="@id='contact'"><xsl:value-of select="$contact" /></xsl:when>
	<xsl:otherwise>
		<span style="color:red">
			WARNING: unnamed &lt;section&gt; element was found with id="<xsl:value-of select="@id" />".<br/>
			The XSLT file(s) should be edited to offer a title for this section.
		</span>
	</xsl:otherwise>
	</xsl:choose>
</xsl:template>

<xsl:template match="/eflash">
<div class="eflash" style="font-family: verdana, Arial, sans-serif;" lang="{lang}" xml:lang="{lang}">
	<xsl:if test="isrtl = 'True'">
		<xsl:attribute name="dir">rtl</xsl:attribute>
	</xsl:if>
	<div style="margin:0.2em 0em 1.3em 0em;"><!--Main heading and description of the eflash-->
		<h1 style="font-family: arial, verdana, sans-serif;margin:0;"><xsl:value-of select="title"/></h1>
		<xsl:if test="description != ''">
			<p style="margin:0;">
				<xsl:value-of disable-output-escaping="yes" select="description"/>
			</p>
		</xsl:if>
	</div>
	<div style="margin:0.2em 0em 1.3em 0em;"><!--Table of content-->
		<ul style="font-size:130%;list-style:none;margin:0;padding:0;font-family: arial, verdana, sans-serif;">
			<xsl:for-each select="sections/section">
			<li>
				<a href="#{@id}">
					<xsl:call-template name="headings"/>
				</a>
				<xsl:if test="@id='inbrief'">
					<ul style="list-style:none;text-indent:20px;margin:0;padding:0;font-size:76%;">
					<xsl:for-each select="news">
						<li>
							<xsl:value-of select="@title"/>
						</li>
					</xsl:for-each>
					</ul>
				</xsl:if>
			</li>
			</xsl:for-each>
		</ul>
	</div>

	<xsl:comment>PDF_GENERATOR_MENU_MARKER</xsl:comment>

	<xsl:for-each select="sections/section">
	<div style="margin:0.2em 0em 1.3em 0em;"><!--Sections from the XML-->
		<a name="{@id}"></a><!--Anchor from the TOC-->
		<h2 style="font-family: arial, verdana, sans-serif;margin:0;"><!--Heading of the sections-->
			<xsl:call-template name="headings"/>
		</h2>
		<xsl:for-each select="*"><!--Loop through the elements in current section-->
		<div lang="{lang}" xml:lang="{lang}" style="margin:0.2em 0em 1.3em 0em;">
			<xsl:if test="@isrtl = 'True'">
				<xsl:attribute name="dir">rtl</xsl:attribute>
			</xsl:if>
			<xsl:if test="name()='news' or name()='document'"><!--heading for news and documents-->
				<h3 style="font-family: arial, verdana, sans-serif;margin:0;">
					<xsl:value-of select="@title"/>
				</h3>
			</xsl:if>
			<xsl:if test="name()='event'"><!--heading for events (includes dates too)-->
				<h3 style="font-family: arial, verdana, sans-serif;margin:0;">
						[<xsl:value-of select="@start_date"/><xsl:if test="@end_date!='None'"> - <xsl:value-of select="@end_date"/></xsl:if>] <xsl:value-of select="@title"/>
				</h3>
			</xsl:if>

			<xsl:if test="name()='event' or name()='training'"><!--Event location-->
				<p style="margin:0;">
					<strong>Address: <xsl:value-of disable-output-escaping="yes" select="address"/></strong>
				</p>
			</xsl:if>

			<p style="margin:0em 0em 0.3em 0em;"><!--Description-->
				<xsl:value-of disable-output-escaping="yes" select="description"/>
			</p>
	
			<p style="margin:0;">
				<xsl:call-template name="trans_further_info" />
				<a href="{@url}"><xsl:call-template name="trans_further_site" /></a>
			</p>

			<xsl:if test="name()='document'"><!--if document display the publisher-->
				<xsl:if test="@publisher!=''">
					<p style="margin:0;"><xsl:call-template name="trans_publisher"/>: <xsl:value-of select="@publisher"/></p>
				</xsl:if>
			</xsl:if>
			
		</div>
		</xsl:for-each>
	</div>
	</xsl:for-each>

	<xsl:comment>PDF_GENERATOR_FOOTER_MARKER</xsl:comment>

	<div style="border-top: 1px solid black; margin: 1em 0em; padding: 1em 0em;"><!--display eFlash information-->
		<p>If you want to UNSUBSCRIBE or change your subscription, <a href="{portal_url}/FlashTool/profilesheet_html">follow this link</a>.</p>
		<p>This is a monthly Flash provided by the EMWIS Technical Unit, to which you can get a free subscription, <a href="{portal_url}/FlashTool/subscribe_html">click here for an open subscription</a>.</p>
		<p>The EMWIS Flash gives you regular and quality information on the water sector in the Euro-Mediterranean area. The main headlines are: Institutions, Documentation, Training, Research and Development, Data Administration. You will also find updates on: international events, relevant web sites, the Euro-Med Water programme of the European Commission, and much more ...</p>
		<p>This Flash exists also in French and Arabic, you can view it and also the previous issues in our <a href="{portal_url}/publications/eflash">website</a>.</p>
		<p>This Flash is also yours, please give us your comments and information to improve it <a href="{portal_url}/feedback_html">Contact the EMWIS Technical Unit</a></p>
		<p>This publication has been produced with the assistance of the European Commission. The content can in no way be taken to reflect the views of the European Union. This disclaimer is not intended to contravene any requirements laid down in applicable national law nor to exclude liability for matters which may not be excluded under that law.</p>
	</div>
</div>
</xsl:template>

</xsl:stylesheet>